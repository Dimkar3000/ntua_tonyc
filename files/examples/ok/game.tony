def t():
    def bool winner(ref char[] board):
        if board[0] = board[1] and board[1] = board[2] and board[0] <> ' ':
            return true
        elsif board[3] = board[4] and board[4] = board[5] and board[3] <> ' ':
            return true
        elsif board[6] = board[7] and board[7] = board[8] and board[6] <> ' ':
            return true
        elsif board[0] = board[3] and board[3] = board[6] and board[0] <> ' ':
            return true
        elsif board[1] = board[4] and board[4] = board[7] and board[1] <> ' ':
            return true
        elsif board[2] = board[5] and board[5] = board[8] and board[2] <> ' ':
            return true
        elsif board[0] = board[4] and board[4] = board[8] and board[0] <> ' ':
            return true
        elsif board[2] = board[4] and board[4] = board[6] and board[2] <> ' ':
            return true
        end
        return false
    end
    def bool ended(ref char[] board):
        int i
        for i:= 0; i<9;i:=i+1:
            if board[i] = ' ':
                return false
            end
        end
        return true
    end
    def int get_move(char current; ref char[] board):
        int i
        char d
        puts("Player \'")
        putc(current)
        puts("\' choose square (1-9): ")
        i:= geti() - 1
        for skip;true;skip:
            if i < 9 and board[i] = ' ':
                puts("You chose the cell: (")
                puti(i / 3)
                puts(", ")
                puti(i mod 3)
                puts(")\n")
                return i
            end
            d := getc()
            puts("\nYou gave wrong number, try again: ")
            i:= geti() - 1
        end
    end

    def switch(ref char current):
        if current = 'X':
            current := 'O'
        else:
            current := 'X' 
        end
    end
    def print_board(ref char[] board):
        int i,j
        
        for i:=0; i<3;i:=i+1:
            puts("-------------\n| ")
            for j:=0; j<3;j:=j+1:
                putc(board[i*3+j])
                puts(" | ")
            end
            putc('\n')
        end
        puts("-------------\n")
    end
    def clear_screen():
        puts("\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n")
    end
    char[] board
    char current, d
    bool win
    int i

    board := new char[9]
    for i:=0; i<9; i:=i+1:
        board[i] := ' '
    end

    current := 'X'

    clear_screen()
    puts("Welcome To the game, press enter to start")
    d := getc()
    for skip;true;skip:
        clear_screen()
        print_board(board)
        i := get_move(current,board)
        board[i] := current
        if winner(board):
            clear_screen()
            print_board(board)
            puts("Player \'")
            putc(current)
            puts("\' Won!!!\n")
            exit
        elsif ended(board):
            puts("Game was a draw\n")
            exit
        end
        switch(current)
    end
end